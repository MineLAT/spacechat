plugins {
    id 'java'
    id 'idea'
    id 'com.gradleup.shadow' version '9.3.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.gradleup.shadow'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        // bstats, caffeine, adventure, mysql-driver, hikaricp, jedis, configurate, protocollib
        mavenCentral()
        // rtag
        maven { url = uri('https://jitpack.io') }
        // spigot
        maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
        maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
        maven { url = uri('https://oss.sonatype.org/content/repositories/central') }
        // localelib
        maven { url = uri('https://repo.codemc.io/repository/maven-public/') }
        // viaversion
        maven { url = uri('https://repo.viaversion.com') }
        // essentials
        maven { url = uri('https://repo.essentialsx.net/releases/') }
        // acf
        maven { url = uri('https://repo.aikar.co/content/groups/aikar/') }
        // placeholderapi
        maven {
            url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
            content {
                includeGroup 'me.clip'
            }
        }
    }

    configurations {
        compileOnlyWith
    }

    dependencies {
        // Compile
        compileOnly('me.clip:placeholderapi:2.11.7') { transitive = false }
        compileOnly('net.essentialsx:EssentialsX:2.21.2') { transitive = false }
        compileOnly 'net.luckperms:api:5.4'

        compileOnly 'org.jetbrains:annotations:26.0.2'


        // Library loader
        implementation 'com.saicone.ezlib:loader:1.3.3'
        compileOnly 'com.saicone.ezlib:annotations:1.3.3'

        // Protocol
        compileOnly 'com.viaversion:viaversion-api:5.7.0'
        compileOnly 'io.netty:netty-all:4.2.9.Final'
        compileOnlyWith 'net.dmulloy2:ProtocolLib:5.4.0'

        // Implementations
        compileOnly 'org.bstats:bstats-bukkit:3.1.0'

        compileOnly 'com.github.ben-manes.caffeine:caffeine:3.2.3'

        compileOnly 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'

        // Only used with reflection
        //compileOnly 'com.mysql:mysql-connector-j:9.5.0'
        compileOnly 'redis.clients:jedis:7.2.0'
        compileOnly 'com.zaxxer:HikariCP:7.0.2'

        compileOnly 'org.spongepowered:configurate-gson:4.2.0'
        compileOnly 'me.pikamug.localelib:LocaleLib:4.1.3'
        compileOnly 'commons-lang:commons-lang:2.6'

        compileOnly 'com.saicone.rtag:rtag:1.5.14'
        compileOnly 'com.saicone.rtag:rtag-item:1.5.14'

        compileOnly('com.github.cryptomorin:XSeries:13.6.0') { transitive = false }
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
    }

    if (project.name != 'paper') {
        tasks.withType(JavaCompile).configureEach {
            classpath += configurations.compileOnlyWith
            options.release = 11
        }
    }

    jar {
        dependsOn (shadowJar)
    }

    shadowJar {
        manifest {
            attributes 'Paperweight-Mappings-Namespace': 'mojang'
        }
        archiveClassifier.set('')

        relocate 'com.saicone.ezlib', project.group + '.lib.ezlib'
        relocate 'org.bstats', project.group + '.lib.bstats'
        relocate 'com.github.benmanes.caffeine.cache', project.group + '.lib.caffeine'
        relocate 'co.aikar.commands', project.group + '.lib.acf.commands'
        relocate 'co.aikar.locales', project.group + '.lib.acf.locales'
        relocate 'com.mysql', project.group + '.lib.mysql'
        relocate 'redis.clients.jedis', project.group + '.lib.jedis'
        relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
        relocate 'org.spongepowered.configurate', project.group + '.lib.configurate'
        relocate 'me.pikamug.localelib', project.group + '.lib.localelib'
        relocate 'com.saicone.rtag', project.group + '.lib.rtag'
        relocate 'com.cryptomorin.xseries', project.group + '.lib.xseries'
    }
}

subprojects {
    dependencies {
        implementation rootProject
    }

    shadowJar {
        archiveBaseName.set(rootProject.name + '-' + project.name)
        destinationDirectory.set(file(rootProject.layout.buildDirectory.dir('libs')))
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(project.properties)
    }
}

dependencies {
    // Compile
    compileOnly 'org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT'

    // Library loader
    annotationProcessor 'com.saicone.ezlib:annotations:1.3.3'

    // Implementations
    compileOnly 'net.kyori:adventure-api:4.26.1'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.4.1'
    compileOnly 'net.kyori:adventure-text-minimessage:4.26.1'
    compileOnly 'net.kyori:adventure-text-serializer-legacy:4.26.1'
}

shadowJar {
    shadowJar {
        archiveBaseName.set(rootProject.name + '-bukkit')
    }

    relocate 'load.adventure', 'true'
    relocate 'net.kyori.adventure', project.group + '.lib.adventure'
    relocate 'net.kyori.examination', project.group + '.lib.examination'
    relocate 'net.kyori.option', project.group + '.lib.option'
}