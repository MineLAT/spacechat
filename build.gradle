plugins {
    id 'java'
    id 'idea'
    id 'io.github.goooler.shadow' version '8.1.7'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(project.properties)
    }
}

repositories {
    // spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // placeholderapi
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // essentials
    maven { url 'https://repo.essentialsx.net/releases/' }
    // triumph-msg
    maven { url 'https://repo.triumphteam.dev/snapshots/' }
    // acf
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    // localelib, rtag
    maven { url 'https://jitpack.io' }

    // bstats, caffeine, adventure, mysql-driver, hikaricp, jedis, configurate
    mavenCentral()
}

dependencies {
    // Compile
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'

    //compileOnly 'com.google.code.gson:gson:2.8.0'
    //compileOnly 'com.google.guava:guava:21.0'
    compileOnly 'org.jetbrains:annotations:24.1.0'


    // Library loader
    implementation 'com.saicone.ezlib:loader:1.2.3'
    compileOnly 'com.saicone.ezlib:annotations:1.2.3'
    annotationProcessor 'com.saicone.ezlib:annotations:1.2.3'

    // Implementations
    compileOnly 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    compileOnly 'net.kyori:adventure-api:4.17.0'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.3.3'
    compileOnly 'net.kyori:adventure-text-minimessage:4.17.0'

    compileOnly 'me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT'

    compileOnly 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'

    // Only used with reflection
    //compileOnly 'mysql:mysql-connector-java:8.0.30'
    compileOnly 'redis.clients:jedis:4.4.6'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    compileOnly 'org.spongepowered:configurate-gson:4.1.2'
    compileOnly 'com.github.PikaMug:LocaleLib:3.9'

    compileOnly 'com.saicone.rtag:rtag:1.5.5'
    compileOnly 'com.saicone.rtag:rtag-item:1.5.5'

    compileOnly('com.github.cryptomorin:XSeries:11.2.0') { transitive = false }
}

compileJava {
    options.release = 11
    options.encoding = 'UTF-8'
}

jar {
    enabled = false
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')

    relocate 'com.saicone.ezlib', project.group + '.lib.ezlib'
    relocate 'org.bstats', project.group + '.lib.bstats'
    relocate 'com.github.benmanes.caffeine.cache', project.group + '.lib.caffeine'
    relocate 'net.kyori.adventure', project.group + '.lib.adventure'
    relocate 'me.mattstudios.msg', project.group + '.lib.msg'
    relocate 'co.aikar.commands', project.group + '.lib.acf.commands'
    relocate 'co.aikar.locales', project.group + '.lib.acf.locales'
    relocate 'com.mysql', project.group + '.lib.mysql'
    relocate 'redis.clients.jedis', project.group + '.lib.jedis'
    relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
    relocate 'org.spongepowered.configurate', project.group + '.lib.configurate'
    relocate 'me.pikamug.localelib', project.group + '.lib.localelib'
    relocate 'com.saicone.rtag', project.group + '.lib.rtag'
    relocate 'com.cryptomorin.xseries', project.group + '.lib.xseries'

    minimize()
}
